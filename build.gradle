plugins {
    id 'base'
    id 'org.jetbrains.kotlin.jvm' version '1.3.71' apply false
    id 'com.github.johnrengelman.shadow' version '5.0.0' apply false
}

ext.versions = [
    'kotlin': '1.3.71',
    'gradle': '6.3',
]

ext.deps = [
    'kotlin': [
        // kotlin stdlib to get kotlin to do anything
        'stdlib': "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"
    ],
    // cli application toolkit
    'clikt': 'com.github.ajalt:clikt:2.6.0'
]

allprojects {
    repositories {
        gradlePluginPortal()
        jcenter()
        maven { url "https://jitpack.io" }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        kotlinOptions {
            jvmTarget = '1.8'
        }
    }
}

tasks.withType(Wrapper.class).configureEach {
  gradleVersion = versions.gradle
  distributionType = Wrapper.DistributionType.BIN
}
