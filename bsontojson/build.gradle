plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'com.github.johnrengelman.shadow'
}

sourceSets {
    main.kotlin.srcDirs += 'src/'
}

jar {
    manifest {
        attributes 'Main-Class': 'com.github.cdxOo.XsonUtils.bsontojson.MainKt'
        attributes 'Implementation-Version': project.property('VERSION_NAME')
    }
}

shadowJar {
    mergeServiceFiles()
}

artifacts {
    archives shadowJar
}

dependencies {
    implementation deps.kotlin.stdlib
    implementation deps.clikt
    
    compile group: 'org.mongodb', name: 'bson', version: '4.0.1'
}

// Implements https://github.com/brianm/really-executable-jars-maven-plugin maven plugin behaviour.
// To check details how it works, see http://skife.org/java/unix/2011/06/20/really_executable_jars.html.
tasks.register("shadowJarExecutable", DefaultTask.class) {
    description = "Creates self-executable file, that runs generated shadow jar"
    group = "Distribution"

    inputs.files tasks.named("shadowJar")
    outputs.file "$buildDir/run/bsontojson"

    doLast {
        File execFile = outputs.files.singleFile
        execFile.withOutputStream {
            it.write "#!/bin/sh\n\nexec java -Xmx512m -jar \"\$0\" \"\$@\"\n\n".bytes
            it.write inputs.files.singleFile.bytes
        }
        execFile.setExecutable(true, false)
    }
}

