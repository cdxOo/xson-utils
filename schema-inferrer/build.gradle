plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'com.github.johnrengelman.shadow'
}

jar {
    manifest {
        attributes 'Main-Class': 'com.github.cdxOo.XsonUtils.schemainferrer.MainKt'
        attributes 'Implementation-Version': project.property('VERSION_NAME')
    }
}

sourceSets {
    main.kotlin.srcDirs += 'src/'
}

shadowJar {
    mergeServiceFiles()
}

artifacts {
    archives shadowJar
}

dependencies {
    implementation deps.kotlin.stdlib
    implementation deps.clikt

    // to read and print json
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.+"

    // the inferrer library
    compile 'com.github.saasquatch:json-schema-inferrer:0.1.1'
    
    // inferrer library needs that
    // https://mvnrepository.com/artifact/commons-validator/commons-validator
    compile group: 'commons-validator', name: 'commons-validator', version: '1.6'

    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    //compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'

}

// Implements https://github.com/brianm/really-executable-jars-maven-plugin maven plugin behaviour.
// To check details how it works, see http://skife.org/java/unix/2011/06/20/really_executable_jars.html.
tasks.register("shadowJarExecutable", DefaultTask.class) {
    description = "Creates self-executable file, that runs generated shadow jar"
    group = "Distribution"

    inputs.files tasks.named("shadowJar")
    outputs.file "$buildDir/run/json-schema-inferrer"

    doLast {
        File execFile = outputs.files.singleFile
        execFile.withOutputStream {
            it.write "#!/bin/sh\n\nexec java -Xmx512m -jar \"\$0\" \"\$@\"\n\n".bytes
            it.write inputs.files.singleFile.bytes
        }
        execFile.setExecutable(true, false)
    }
}

